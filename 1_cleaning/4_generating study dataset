********************************************************************************

* Creating the master dataset for patterns of prescribing project including patterns, class info, indication info and covariate info

* Author: Flo Martin

* Date: 06/08/2024

********************************************************************************

* Datasets generated by this do-file

* 	- $Datadir\table_2_dataset.dta

********************************************************************************

* Start logging 

	log using "$Logdir\1_cleaning\4_generating study dataset", name(generating_study_dataset) replace
	
********************************************************************************

* Load pregnancy cohort

	use "$Deriveddir\derived_data\pregnancy_cohort_final.dta", clear // from population derivation scripts
	keep patid pregid updated_outcome // keep updated outcome so we can restrict to known outcome pregnancies
	
	* Exposure
	
	merge 1:1 patid pregid using "$Datadir\patterns_in_pregnancy_clean_withclasses.dta", keepusing(class* dosage* any_*) keep(3) nogen
	
	rename any_preg any_preg_wide
	label drop class_lb
	
	merge 1:1 patid pregid using "$Datadir\patterns_in_pregnancy_long.dta", keep(3) nogen
	
	replace any_b=. if any_preg ==0 & any_preg_wide ==1
	replace class_t2 = 0 if any_preg ==0 & any_preg_wide ==1
	replace class_preg = 0 if any_preg ==0 & any_preg_wide ==1
	replace any_preg=0 if any_preg ==0 & any_preg_wide ==1
	
	tab any_preg any_preg_wide
	drop any_preg_wide
	
	/* Antidepressant use in the 12 months prior to pregnancy
	
	gen any_prepreg = 1 if any_l==1 | any_m==1 | any_n==1 | any_o==1
	replace any_prepreg = 0 if any_prepreg==.
	tab any_prepreg
	
	* Antidepressant use in the 12 months after to pregnancy
	
	gen any_postpreg = 1 if any_w==1 | any_x==1 | any_y==1 | any_z==1
	replace any_postpreg = 0 if any_postpreg==.
	tab any_postpreg */
	
	tab any_postpreg discontinuer // 1,097 continuers who have no post-pregnancy prescriptions
	
	br if any_postpreg==0 & discontinuer==0
	
	/*preserve
	
		keep if any_postpreg==0 & discontinuer==0
		keep patid pregid
		save "$Tempdatadir\weird continuers", replace
		
		merge 1:m patid pregid using "$Tempdatadir\long_by_week.dta", keep(3) nogen
		
		br
		
			* All discontinued within two weeks of delivery so recode to being exposed in any_w?
			
		erase "$Tempdatadir\weird continuers.dta"
	
	restore*/
	
	replace any_w = 1 if any_postpreg==0 & discontinuer==0 // last prescription ended less than 2 weeks before the pregnancy ended => any_w changed to 1 from zero
	
	replace any_postpreg = 1 if any_w==1 
	
	tab any_postpreg discontinuer
	
	* Discontinuation in the 12 months prior to pregnancy
	
	tab any_prepreg any_preg
	
	gen prepreg_discont = 1 if any_prepreg==1 & any_preg==0
	replace prepreg_discont = 0 if any_prepreg==1 & any_preg==1
	tab prepreg_discont
	
	gen late_prepreg_discont = 1 if prepreg_discont==1 & any_o==1
	replace late_prepreg_discont = 0 if prepreg_discont==1 & any_o==0
	tab late_prepreg_discont
	
	* No use before or during pregnancy
	
	gen no_use = 1 if any_prepreg==0 & any_preg==0
	replace no_use = 0 if any_prepreg==1 | any_preg==1
	tab no_use
	
	* Covariates
	
	merge 1:1 patid pregid using "$Deriveddir\covariates\pregnancy_cohort_covariates.dta", keep(1 3) nogen
	count
	
	* Indications
	
		* Binary depression from primary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_depression_read.dta", keep(1 3) nogen
	count
	
		* Binary anxiety from primary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_anxiety_read.dta", keep(1 3) nogen
	count
	
		* Binary depression from secondary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_depression_hes.dta", keep(1 3) nogen
	count
	
		* Binary anxiety from secondary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_anxiety_hes.dta", keep(1 3) nogen
	count
	
		* Binary other AD indications from primary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_other_indics_read.dta", keep(1 3) nogen
	count
	
		* Binary other AD indications from secondary care
	merge 1:1 patid pregid using "$Deriveddir\indications\all_other_indics_hes.dta", keep(1 3) nogen
	count
	
	* Drop missing outcome or unknown outcome pregnancies
	tab updated_outcome, m
	drop if updated_outcome==13 | updated_outcome==.
	count
	
	* Depression ever before the end of pregnancy
	
	rename depression depression_read
	gen depression =1 if depression_read==1 | depression_hes==1 | depression_preg==1 | depression_preg_hes==1
	replace depression = 0 if depression==.
	tab depression any_preg, col
	
	label variable depression"Depression noted in CPRD/HES ever before the end of pregnancy"
	
	replace depression_ever = 1 if depression_ever_hes==1
	replace depression_12mo = 1 if depression_12mo_hes==1
	replace depression_preg = 1 if depression_preg_hes==1
	
	* Anxiety ever before the end of pregnancy

	rename anxiety anxiety_read
	gen anxiety =1 if anxiety_read==1 | anxiety_hes==1 | anxiety_preg==1 | anxiety_preg_hes==1
	replace anxiety = 0 if anxiety==.
	tab anxiety any_preg, col
	
	label variable anxiety"Anxiety noted in CPRD/HES ever before the end of pregnancy"
	
	replace anxiety_ever = 1 if anxiety_ever_hes==1
	replace anxiety_12mo = 1 if anxiety_12mo_hes==1
	replace anxiety_preg = 1 if anxiety_preg_hes==1
	
	* Other affective / mood disorders before the end of pregnancy
	
	rename mood mood_read
	gen mood = 1 if mood_read==1 | affective_hes==1 | mood_preg==1 | affective_preg_hes==1
	replace mood = 0 if mood==.
	tab mood any_preg, col
	
	label variable mood"Other mood disorders with a depressive element noted in CPRD/HES ever before the end of pregnancy"
	
	replace mood_ever = 1 if affective_ever_hes==1
	replace mood_12mo = 1 if affective_12mo_hes==1
	replace mood_preg = 1 if affective_preg_hes==1
	
	* Eating disorders
	
	rename ed ed_read
	gen ed = 1 if ed_read==1 | ed_hes==1 | ed_preg==1 | ed_preg_hes==1
	replace ed = 0 if ed==.
	tab ed any_preg, col
	
	label variable ed"Eating disorder noted in CPRD/HES ever before the end of pregnancy"
	
	replace ed_ever = 1 if ed_ever_hes==1
	replace ed_12mo = 1 if ed_12mo_hes==1
	replace ed_preg = 1 if ed_preg_hes==1
	
	* Pain
	
	rename pain pain_read
	gen pain = 1 if pain_read==1 | pain_hes==1 | pain_preg==1 | pain_preg_hes==1
	replace pain = 0 if pain==.
	tab pain any_preg, col
	
	label variable pain"Pain noted in CPRD/HES ever before the end of pregnancy"
	
	replace pain_ever = 1 if pain_ever_hes==1
	replace pain_12mo = 1 if pain_12mo_hes==1
	replace pain_preg = 1 if pain_preg_hes==1
	
	* Tension-type headache
	
	rename headache headache_read
	gen headache = 1 if headache_read==1 | tt_headache_hes==1 | headache_preg==1 | tt_headache_preg_hes==1
	replace headache = 0 if headache==.
	tab headache any_preg, col
	
	label variable headache"Tension-type headache noted in CPRD/HES ever before the end of pregnancy"
	
	replace headache_ever = 1 if tt_headache_ever_hes==1
	replace headache_12mo = 1 if tt_headache_12mo_hes==1
	replace headache_preg = 1 if tt_headache_preg_hes==1
	
	* Diabetic neuropathy
	
	rename dn dn_read
	gen dn = 1 if dn_read==1 | dn_hes==1 | dn_preg==1 | dn_preg_hes==1
	replace dn = 0 if dn==.
	tab dn any_preg, col
	
	label variable dn"Diabetic neuropathy noted in CPRD/HES ever before the end of pregnancy"
	
	replace dn_ever = 1 if dn_ever_hes==1
	replace dn_12mo = 1 if dn_12mo_hes==1
	replace dn_preg = 1 if dn_preg_hes==1
	
	* Migraine prophylaxis
	
	rename migraine migraine_read
	gen migraine = 1 if migraine_read==1 | migraine_hes==1 | migraine_preg==1 | migraine_preg_hes==1
	replace migraine = 0 if migraine==.
	tab migraine any_preg, col
	
	label variable migraine"Migraine prophylaxis noted in CPRD/HES ever before the end of pregnancy"
	
	replace migraine_ever = 1 if migraine_ever_hes==1
	replace migraine_12mo = 1 if migraine_12mo_hes==1
	replace migraine_preg = 1 if migraine_preg_hes==1
	
	* Stress incontinence
	
	rename incont incont_read
	gen incont = 1 if incont_read==1 | stress_incont_hes==1 | incont_preg==1 | stress_incont_preg_hes==1
	replace incont = 0 if incont==.
	tab incont any_preg, col
	
	label variable incont"Stress incontinence noted in CPRD/HES ever before the end of pregnancy"
	
	replace incont_ever = 1 if stress_incont_ever_hes==1
	replace incont_12mo = 1 if stress_incont_12mo_hes==1
	replace incont_preg = 1 if stress_incont_preg_hes==1
	
	* Exposed/unexposed variables
	gen exposed = 1 if any_preg==1
	gen unexposed = 1 if any_preg==0
	
	replace any_preg = 0 if any_preg!=1
	
	* Save for Table 2 do-file
	
	rename class_t1 class_a
	rename class_t2 class_b
	rename class_t3 class_c
	
	save "$Datadir\table_2_dataset.dta", replace
	
********************************************************************************

* Stop logging, translate .smcl into .pdf and erase .smcl

	log close generating_study_dataset
	
	translate "$Logdir\1_analysis\4_generating study dataset.smcl" "$Logdir\1_analysis\4_generating study dataset.pdf", replace
	
	erase "$Logdir\1_analysis\4_generating study dataset.smcl"
	
********************************************************************************
